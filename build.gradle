import org.gradle.internal.jvm.Jvm

import java.time.Instant
import java.time.ZoneId

buildscript {
    ext {
        es_version = version.replaceAll(/\.[0-9]+(|-.+)$/, "")
    }

    repositories {
        mavenLocal()
        mavenCentral()
    }

    dependencies {
        classpath "org.elasticsearch.gradle:build-tools:${es_version}"
        classpath group: 'javax.xml.bind', name: 'jaxb-api', version: '2.3.1'
        classpath group: 'com.sun.xml.bind', name: 'jaxb-core', version: '4.0.2'
        classpath group: 'com.sun.xml.bind', name: 'jaxb-impl', version: '4.0.2'
        classpath group: 'javax.activation', name: 'activation', version: '1.1.1'
    }
}

plugins {
    id "java"
    id "checkstyle"
    id "com.github.ben-manes.versions" version "0.46.0"
}

apply plugin: 'java'
apply plugin: 'idea'
apply plugin: 'elasticsearch.esplugin'
apply plugin: 'elasticsearch.yaml-rest-test'
apply plugin: 'elasticsearch.testclusters'

// Uncomment if you want to use: System.out.println("Emergency!");
// Logs are found in build/testcluster/integTest-*/logs/ folder.
//forbiddenApis {
//    ignoreFailures = true
//}

// No unit tests in this plugin
test.enabled = false

println "Host: " + InetAddress.getLocalHost()
println "Gradle: " + gradle.gradleVersion + " JVM: " + Jvm.current() + " Groovy: " + GroovySystem.getVersion()
println "Build: group: '${project.group}', name: '${project.name}', version: '${project.version}'"
println "Timestamp: " + Instant.now().atZone(ZoneId.systemDefault()).toString()

repositories {
    mavenCentral()
    mavenLocal()
}

ext {
    versions = [
        "elasticsearch": es_version,
        "prometheus"   : "0.16.0",
        "log4j"        : "2.19.0",
        "junit"        : "4.13.2"
    ]
}

configurations {
    runtime
    releaseJars {
        extendsFrom runtime
        exclude group: "org.elasticsearch"
        exclude group: "com.fasterxml.jackson.core", module: "jackson-core"
        exclude group: "org.apache.logging.log4j"
    }
}

dependencies {
    implementation "org.elasticsearch:elasticsearch:${versions.elasticsearch}"
    implementation "io.prometheus:simpleclient:${versions.prometheus}"
    implementation "io.prometheus:simpleclient_common:${versions.prometheus}"
    implementation "io.prometheus:simpleclient_hotspot:${versions.prometheus}"
    yamlRestTestRuntimeOnly "org.apache.logging.log4j:log4j-core:${versions.log4j}"
    testImplementation (group: 'junit', name: 'junit', version: "${versions.junit}") {
    exclude group:'org.hamcrest' //also included in ES test framework
    }
    releaseJars "${project.group}:${project.name}:${project.version}"
}

tasks.withType(JavaCompile) {
    options.compilerArgs << "-Xlint:unchecked,deprecation"
}

esplugin {
    licenseFile rootProject.file('LICENSE.txt')
    noticeFile rootProject.file('NOTICE.txt')
    name pluginName
    description pluginDescription
    classname pluginClassname
}

testClusters.all {
    numberOfNodes = 2
}

checkstyle {
    configFile = new File(rootDir, "checkstyle.xml")
    toolVersion = "8.2"
}
